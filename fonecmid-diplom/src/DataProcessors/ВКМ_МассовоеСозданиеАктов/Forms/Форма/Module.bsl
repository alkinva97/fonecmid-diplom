
#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура Заполнить(Команда)
	
	ДлительнаяОперация = НачатьВыполнениеНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Функция НачатьВыполнениеНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
	"Обработки.ВКМ_МассовоеСозданиеАктов.РассчитатьЗначение", 
	Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРезультат(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиРезультат(АдресРезультата)
	
	Объект.СписокДоговоров.Очистить();
	
	СтрокаJSON = ПолучитьИзВременногоХранилища(АдресРезультата);
	СсылкиНаОбъекты = ПрочитатьЗначениеJSON(СтрокаJSON);
	Для Каждого Структура Из СсылкиНаОбъекты Цикл
		
		ДоговорСсылка = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку(Новый УникальныйИдентификатор(Структура.Договор));
		РеализацияСсылка = Документы.РеализацияТоваровУслуг.ПолучитьСсылку(Новый УникальныйИдентификатор(Структура.Реализация));			
		НоваяСтрокаТЧ = Объект.СписокДоговоров.Добавить();
		НоваяСтрокаТЧ.Договор = ДоговорСсылка;
		НоваяСтрокаТЧ.Реализация = РеализацияСсылка;		
		
	КонецЦикла;
			
КонецПроцедуры

#КонецОбласти

