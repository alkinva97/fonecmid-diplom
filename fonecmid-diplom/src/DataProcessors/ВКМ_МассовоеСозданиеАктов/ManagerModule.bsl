
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает JSON-строку содержащую уникальные идентификаторы ссылкок на объекты справочника ДоговорыКонтрогентов
// и документа РеализацияТоваровИУслуг
//
// Параметры:
//  ДатаНачала  - Дата - начало месяца
//  ДатаОкончания  - Дата - конец месяца
//
// Возвращаемое значение:
//  Строка - JSON-строкe
//
Функция РассчитатьЗначение(ДатаНачала, ДатаОкончания) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыКонтрагентов.Ссылка КАК Договор,
	               |	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	               |	ДоговорыКонтрагентов.Организация КАК Организация,
	               |	ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора КАК НачалоДействия,
	               |	ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора КАК ОкончаниеДействия,
	               |	РеализацияТоваровУслуг.Ссылка КАК Реализация
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |		ПО ДоговорыКонтрагентов.Ссылка = РеализацияТоваровУслуг.Договор
	               |			И ДоговорыКонтрагентов.Владелец = РеализацияТоваровУслуг.Контрагент
	               |			И ДоговорыКонтрагентов.Организация = РеализацияТоваровУслуг.Организация
	               |			И (РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
	               |ГДЕ
	               |	ДоговорыКонтрагентов.ВидДоговора = &АбонентскоеОбслуживание
	               |	И ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора >= &ДатаОкончания
	               |	И ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора <= &ДатаНачала";
	
	Запрос.УстановитьПараметр("АбонентскоеОбслуживание", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	СсылкиНаОбъекты = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Реализация) Тогда
			ДоговорУникальныйИдент = Строка(ВыборкаДетальныеЗаписи.Договор.УникальныйИдентификатор());
			РеализацияУникальныйИдент = Строка(ВыборкаДетальныеЗаписи.Реализация.УникальныйИдентификатор());
			
			ДанныеДляТабЧасти = Новый Структура;
			ДанныеДляТабЧасти.Вставить("Договор", ДоговорУникальныйИдент);
			ДанныеДляТабЧасти.Вставить("Реализация", РеализацияУникальныйИдент);
				
			СсылкиНаОбъекты.Добавить(ДанныеДляТабЧасти);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Реализация) Тогда
			НовыйДок = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			НовыйДок.Дата = ДатаОкончания;
			НовыйДок.Договор = ВыборкаДетальныеЗаписи.Договор;
			НовыйДок.Контрагент = ВыборкаДетальныеЗаписи.Контрагент;
			НовыйДок.Организация = ВыборкаДетальныеЗаписи.Организация;
			НовыйДок.ВыполнитьАвтозаполнение(НовыйДок.Услуги, ДатаНачала, ДатаОкончания);
			НовыйДок.Комментарий = "Создан обработкой Массовое создание актов";
			Попытка
				НовыйДок.Записать();
				Если Не НовыйДок.ПроверитьЗаполнение() Тогда
					Продолжить;
				КонецЕсли;
				НовыйДок.Записать(РежимЗаписиДокумента.Проведение);
				НовыйДокСсылка = НовыйДок.Ссылка;
	
				ДоговорУникальныйИдент = Строка(ВыборкаДетальныеЗаписи.Договор.УникальныйИдентификатор());
				РеализацияУникальныйИдент = Строка(НовыйДокСсылка.УникальныйИдентификатор());
				
				ДанныеДляТабЧасти = Новый Структура;
				ДанныеДляТабЧасти.Вставить("Договор", ДоговорУникальныйИдент);
				ДанныеДляТабЧасти.Вставить("Реализация", РеализацияУникальныйИдент);
				
				СсылкиНаОбъекты.Добавить(ДанныеДляТабЧасти);
			Исключение
				ВызватьИсключение;
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЗаписатьЗначениеJSON(СсылкиНаОбъекты);
	
КонецФункции

#КонецОбласти

#КонецЕсли
