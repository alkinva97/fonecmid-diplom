
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий 

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВКМ_ОтпускаСотрудников.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
		|	СУММА(РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала, ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания, ДЕНЬ), ДЕНЬ, 1), ДЕНЬ)) КАК КоличествоДнейОтпуска
		|ИЗ
		|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
		|ГДЕ
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОтпускаСотрудников.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Год = НачалоГода(Год);
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.КоличествоДней = Выборка.КоличествоДнейОтпуска;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
  	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли