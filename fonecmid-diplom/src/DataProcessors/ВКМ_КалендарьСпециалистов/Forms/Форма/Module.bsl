
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПерОтображения = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецПерОтображения = КонецДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	
	Объект.Календарь.ТекущиеПериодыОтображения.Добавить(НачалоПерОтображения, КонецПерОтображения);
	
	ОбновитьКалендарьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВКМ_ОбслуживаниеКлиента" Тогда 
		ОбновитьКалендарьНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы 

&НаКлиенте
Процедура КалендарьПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ДатаПроведенияРабот", НачалоДня(Начало));
	ЗначенияЗаполнения.Вставить("ВремяНачалаРаботПлан", Начало);
	ЗначенияЗаполнения.Вставить("ВремяОкончанияРаботПлан", Конец);
	ЗначенияЗаполнения.Вставить("Специалист", ЗначенияИзмерений["Специалисты"]);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.Форма.ФормаДокумента", ПараметрыФормы);		
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ОбновитьКалендарь(Команда)
	
	ОбновитьКалендарьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура ОбновитьКалендарьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_Сотрудники.Ссылка КАК Сотрудники,
	|	ВКМ_Сотрудники.Представление КАК Представление
	|ИЗ
	|	Справочник.ВКМ_Сотрудники КАК ВКМ_Сотрудники
	|ГДЕ
	|	ВКМ_Сотрудники.Категория = &Специалисты";
	
	Запрос.УстановитьПараметр("Специалисты", Перечисления.ВКМ_КатегорииСотрудников.Специалист);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ИзмеренияПланировщика = Объект.Календарь.Измерения;
	ИзмеренияПланировщика.Очистить();
	ИзмерениеСпециалисты = ИзмеренияПланировщика.Добавить("Специалисты");
	
	Пока Выборка.Следующий() Цикл
		
		НовыйСпециалист = ИзмерениеСпециалисты.Элементы.Добавить(Выборка.Сотрудники);
		НовыйСпециалист.Текст = Выборка.Представление;		
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиента.Ссылка КАК Ссылка,
	|	ВКМ_ОбслуживаниеКлиента.Специалист.Представление КАК СпециалистПредставление,
	|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
	|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан КАК ВремяНачалаРаботПлан,
	|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан КАК ВремяОкончанияРаботПлан,
	|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЭлементыПланировщика = Объект.Календарь.Элементы;
	ЭлементыПланировщика.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		ДатаПроведенияРабот = Выборка.ДатаПроведенияРабот;
		СтрокаXML = XMLСтрока(ДатаПроведенияРабот);
		ПозицияРазделителя = СтрНайти(СтрокаXML, "T");
		Дата = Лев(СтрокаXML, ПозицияРазделителя - 1);
		
		ВремяНачалаРаботПлан = Выборка.ВремяНачалаРаботПлан;
		СтрокаXML = XMLСтрока(ВремяНачалаРаботПлан);
		ПозицияРазделителя = СтрНайти(СтрокаXML, "T");
		ВремяНачала = Сред(СтрокаXML, ПозицияРазделителя + 1); 
		
		ВремяОкончанияРаботПлан = Выборка.ВремяОкончанияРаботПлан;
		СтрокаXML = XMLСтрока(ВремяОкончанияРаботПлан);
		ПозицияРазделителя = СтрНайти(СтрокаXML, "T");
		ВремяОкончания = Сред(СтрокаXML, ПозицияРазделителя + 1);
		
		ДатаВремяНачала = XMLЗначение(Тип("Дата"), Дата + "T" + ВремяНачала);
		ДатаВремяОкончания = XMLЗначение(Тип("Дата"), Дата + "T" + ВремяОкончания);
		
		СоответствиеЗначений = Новый Соответствие;
		СоответствиеЗначений.Вставить("Специалисты", Выборка.Специалист);
		
		ЭлементПланировщика = ЭлементыПланировщика.Добавить(ДатаВремяНачала, ДатаВремяОкончания);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеЗначений);
		ЭлементПланировщика.Текст = Выборка.СпециалистПредставление;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
